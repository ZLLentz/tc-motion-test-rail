<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4020.14">
  <POU Name="FB_MicroStepWalkTest" Id="{47ade49f-de9a-455c-80d5-22d90c7fdc83}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_MicroStepWalkTest
VAR_INPUT
	bExecute: BOOL;
	nSteps: UINT;
	fDeadband: LREAL;
	fLowerBound: LREAL;
	fUpperBound: LREAL;
	fVelocity: LREAL;
	tSettleTime: TIME;
END_VAR
VAR_OUTPUT
	nGood: UINT;
	nBad: UINT;
	fPercent: LREAL;
	fAvgErr: LREAL;
	fMaxErr: LREAL;
	fMinErr: LREAL := 9001;
END_VAR
VAR_IN_OUT
	Axis: AXIS_REF;
END_VAR
VAR
	fbRand: DRAND;
	fbMove: MC_MoveAbsolute;
	fbSettleTimer: TON;
	bDoMove: BOOL;
	fGoal: LREAL;
	fStepErr: LREAL;
	fTotalErr: LREAL;
	nStepCounter: UINT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// Moves to random positions and see how close we get to each one
// Pick random numbers from 0 to 1
fbRand(Seed:=UINT_TO_INT(nSteps));
// Latch a new random goal if we aren't moving
IF NOT bDoMove THEN
	fGoal := fbRand.Num * (fUpperBound - fLowerBound) + fLowerBound;
END_IF
// Motion FB
fbMove(Axis:=Axis,
	Execute:=bDoMove,
	Position:=fGoal,
	Velocity:=fVelocity);
// Settle time
fbSettleTimer(IN:=fbMove.Done,
	PT:=tSettleTime);
// Re-enable the move for next cycle
bDoMove := bExecute AND nStepCounter < nSteps;
// Check results
IF fbSettleTimer.Q THEN
	fStepErr := ABS(Axis.NcToPlc.PosDiff);
	IF fStepErr <= fDeadband THEN
		nGood := nGood + 1;
	ELSE
		nBad := nBad + 1;
	END_IF
	IF nGood + nBad > 0 THEN
		fPercent := 100 * nGood / (nGood + nBad);
	END_IF
	fTotalErr := fTotalErr + fStepErr;
	nStepCounter := nStepCounter + 1;
	IF nStepCounter > 0 THEN
		fAvgErr := fTotalErr / nStepCounter;
	END_IF
	IF fStepErr > fMaxErr THEN
		fMaxErr := fStepErr;
	END_IF
	IF fStepErr < fMinErr THEN
		fMinErr := fStepErr;
	END_IF
	// Reset the move block
	bDoMove := FALSE;
END_IF]]></ST>
    </Implementation>
    <LineIds Name="FB_MicroStepWalkTest">
      <LineId Id="36" Count="0" />
      <LineId Id="13" Count="0" />
      <LineId Id="33" Count="0" />
      <LineId Id="24" Count="0" />
      <LineId Id="28" Count="1" />
      <LineId Id="41" Count="4" />
      <LineId Id="53" Count="1" />
      <LineId Id="56" Count="0" />
      <LineId Id="87" Count="1" />
      <LineId Id="58" Count="0" />
      <LineId Id="66" Count="0" />
      <LineId Id="95" Count="0" />
      <LineId Id="67" Count="0" />
      <LineId Id="69" Count="0" />
      <LineId Id="72" Count="1" />
      <LineId Id="71" Count="0" />
      <LineId Id="75" Count="0" />
      <LineId Id="74" Count="0" />
      <LineId Id="76" Count="2" />
      <LineId Id="80" Count="0" />
      <LineId Id="79" Count="0" />
      <LineId Id="82" Count="0" />
      <LineId Id="93" Count="0" />
      <LineId Id="96" Count="1" />
      <LineId Id="99" Count="2" />
      <LineId Id="85" Count="1" />
      <LineId Id="68" Count="0" />
      <LineId Id="9" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>